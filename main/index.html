<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml"><head><title>index (patricia-tree.index)</title><meta charset="utf-8"/><link rel="stylesheet" href="../odoc.support/odoc.css"/><meta name="generator" content="odoc 2.4.2"/><meta name="viewport" content="width=device-width,initial-scale=1.0"/><script src="../odoc.support/highlight.pack.js"></script><script>hljs.initHighlightingOnLoad();</script><script>let base_url = '../';
let search_urls = ['db.js','../sherlodoc.js'];
</script><script src="../odoc.support/odoc_search.js" defer="defer"></script></head><body class="odoc"><nav class="odoc-nav"><a href="../index.html">Up</a> â€“ patricia-tree</nav><div class="odoc-search"><div class="search-inner"><input class="search-bar" placeholder="ðŸ”Ž Search..."/><div class="search-snake"></div><div class="search-result"></div></div></div><header class="odoc-preamble"><h1 id="package-patricia-tree"><a href="#package-patricia-tree" class="anchor"></a>Package patricia-tree</h1><p>This library contains a single module: <a href="PatriciaTree/index.html"><code>PatriciaTree</code></a>.</p><p>This is version <code>0.9.0</code> of the library. It is known to work with OCaml versions ranging from <code>4.14</code> to <code>5.2</code>.</p><p>This is an <a href="https://ocaml.org/">OCaml</a> library that implements sets and maps as Patricia Trees, as described in Okasaki and Gill's 1998 paper <a href="https://www.semanticscholar.org/paper/Fast-Mergeable-Integer-Maps-Okasaki-Gill/23003be706e5f586f23dd7fa5b2a410cc91b659d"><i>Fast mergeable integer maps</i></a>. It is a space-efficient prefix trie over the big-endian representation of the key's integer identifier.</p><p>The source code of this library is available <a href="https://github.com/codex-semantics-library/patricia-tree">on Github</a> under an <a href="https://choosealicense.com/licenses/lgpl-2.1/">LGPL-2.1</a> license.</p><p>This library was written by <a href="https://www.researchgate.net/profile/Matthieu-Lemerre">Matthieu Lemerre</a>, then further improved by <a href="https://www.normalesup.org/~dlesbre/">Dorian Lesbre</a>, as part of the <a href="Codex semantics library">https://codex.top/</a>, developed at <a href="https://list.cea.fr/en/">CEA List</a>.</p></header><nav class="odoc-toc"><ul><li><a href="#installation">Installation</a></li><li><a href="#features">Features</a></li></ul></nav><div class="odoc-content"><h2 id="installation"><a href="#installation" class="anchor"></a>Installation</h2><p>This library can be installed with <a href="https://opam.ocaml.org/">opam</a>:</p><pre class="language-bash"><code>opam install patricia-tree</code></pre><p>Alternatively, you can clone the source repository and install with <a href="https://dune.build/">dune</a>:</p><pre class="language-bash"><code>git clone git@github.com:codex-semantics-library/patricia-tree.git
opam install . --deps-only
cd patricia-tree
dune build
dune install
# To build documentation
opam install odoc
dune build @doc</code></pre><h2 id="features"><a href="#features" class="anchor"></a>Features</h2><ul><li>Similar to OCaml's <a href="https://ocaml.org/api/Map.S.html"><code>Map</code></a> and <a href="https://ocaml.org/api/Set.S.html"><code>Set</code></a>, using the same function names when possible and the same convention for order of arguments. This should allow switching to and from Patricia Tree with minimal effort.</li><li>The functor parameters (<a href="PatriciaTree/module-type-KEY/index.html"><code>PatriciaTree.KEY</code></a> module) requires an injective <code>to_int : t -&gt; int</code> function instead of a <code>compare</code> function. <a href="PatriciaTree/module-type-KEY/index.html#val-to_int"><code>PatriciaTree.KEY.to_int</code></a> should be fast, and injective. This works well with <a href="https://en.wikipedia.org/wiki/Hash_consing">hash-consed</a> types.</li><li><p>The Patricia Tree representation is stable, contrary to maps, inserting nodes in any order will return the same shape. This allows different versions of a map to share more subtrees in memory, and the operations over two maps to benefit from this sharing. The functions in this library attempt to <b>maximally preserve sharing and benefit from sharing</b>, allowing very important improvements in complexity and running time when combining maps or sets is a frequent operation.</p><p>To do so, these functions often have extra requirements on their argument (e.g. <code>inter f m1 m2</code> can be optimized by not inspecting common subtrees when <code>f</code> is idempotent). To avoid accidental errors, they are renamed (e.g. to <code>idempotent_inter</code> for the efficient version and <code>nonidempotent_inter_no_share</code> for the general one)</p></li><li><p>Since our Patricia Tree use big-endian order on keys, the maps and sets are sorted in increasing <b><a href="PatriciaTree/index.html#val-unsigned_lt" title="PatriciaTree.unsigned_lt">unsigned order</a></b> of keys. This means negative keys are sorted above positive keys, with <code>-1</code> being the largest possible key, and <code>0</code> the smallest. This also avoids a bug in Okasaki's paper discussed in <a href="https://www.cs.tufts.edu/comp/150FP/archive/jan-midtgaard/qc-patricia.pdf"><i>QuickChecking Patricia Trees</i></a> by Jan Mitgaard.</p><p>It also affects functions like <a href="PatriciaTree/module-type-BASE_MAP/index.html#val-unsigned_min_binding" title="PatriciaTree.BASE_MAP.unsigned_min_binding"><code>unsigned_min_binding</code></a> and <a href="PatriciaTree/module-type-BASE_MAP/index.html#val-pop_unsigned_minimum" title="PatriciaTree.BASE_MAP.pop_unsigned_minimum"><code>pop_unsigned_minimum}. They will return the smallest
  positive integer of both positive and negative keys are present; and not the smallest negative,
  as one might expect.}
{li Supports generic maps and sets: a ['m map] that maps ['k key] to [('k, 'm) value].
  This is especially useful when using {{: https://v2.ocaml.org/manual/gadts-tutorial.html}GADTs}
  for the type of keys. This is also sometimes called a dependent map.}
{li Allows easy and fast operations across different types of maps and set
  which have the same type of keys (e.g. an intersection between a map and a set).}
{li Multiple choices for internal representation ({!PatriciaTree.NODE}), which allows for efficient
  storage (no need to store a value for sets), or using weak nodes only (values removed from the tree if no other pointer to it exists). This system can also
  be extended to store size information in nodes if needed.}
{li Exposes a common interface ({!type:PatriciaTree.NODE.view}) to allow users to write their own pattern
  matching on the tree structure without depending on the {!PatriciaTree.NODE} being used.}}
{1 Quick overview}
{2 Functors}
This library contains a single module, {!PatriciaTree}.
The functors used to build maps and sets are the following:
{ul
{li For homogeneous (non-generic) maps and sets: {!PatriciaTree.MakeMap} and
    {!PatriciaTree.MakeSet}. These are similar to the standard library's maps and sets.
    {[
      module MakeMap(Key: KEY) : MAP with type key = Key.t
      module MakeSet(Key: KEY) : SET with type elt = Key.t
    ]}}
{li For Heterogeneous (generic) maps and sets: {!PatriciaTree.MakeHeterogeneousMap}
    and {!PatriciaTree.MakeHeterogeneousSet}.
    {[
      module MakeHeterogeneousMap(Key: HETEROGENEOUS_KEY)(Value: VALUE) : HETEROGENEOUS_MAP
        with type 'a key = 'a Key.t
         and type ('k,'m) value = ('k,'m) Value.t
      module MakeHeterogeneousSet(Key: HETEROGENEOUS_KEY) : HETEROGENEOUS_SET
        with type 'a elt = 'a Key.t
    ]}}
}

{2 Interfaces}
Here is a brief overview of the various module types of our library:
{ul
{li {!PatriciaTree.BASE_MAP}: the underlying module type of all our trees (maps end sets). It
  represents a ['b map] binding ['a key] to [('a,'b) value], as well as all
  functions needed to manipulate them.
  It can be accessed from any of the more specific maps types, thus providing a
  unified representation, useful for cross map operations. However, for practical
  purposes, it is often best to use the more specific interfaces:
  {ul
  {li {!PatriciaTree.HETEROGENEOUS_MAP} for heterogeneous maps (this is just [BASE_MAP] with a
    [WithForeign] functor).}
  {li {!PatriciaTree.MAP} for homogeneous maps, this interface is close to {{: https://ocaml.org/api/Map.S.html}[Stdlib.Map.S]}.}
  {li {!PatriciaTree.HETEROGENEOUS_SET} for heterogeneous sets (sets of ['a elt]). These are just
    maps to [unit], but with a custom node representation to avoid storing [unit] in
    nodes.}
  {li {!PatriciaTree.SET} for homogeneous sets, this interface is close to {{: https://ocaml.org/api/Set.S.html}[Stdlib.Set.S]}.}
  }}
{li The parameter of our functor are either {!PatriciaTree.KEY} or {!PatriciaTree.HETEROGENEOUS_KEY}.
  These just consist of a type, a (polymorphic) equality function, and an
  injective [to_int] coercion.
  The heterogeneous map functor also has a {!PatriciaTree.VALUE} parameter to specify the
  [('a, 'b) value] type.}
{li The internal representations of our tree can be customized to use different
  internal {!PatriciaTree.NODE}. Each node come with its own private constructors and destructors,
  as well as a cast to a uniform {!type:PatriciaTree.NODE.view} type used for pattern matching.
  A number of implementations are provided {!PatriciaTree.SimpleNode} (exactly the {!type:PatriciaTree.NODE.view} type),
  {!PatriciaTree.WeakNode} (node which only store weak pointer to its elements), {!PatriciaTree.NodeWithId}
  (node which contain a unique identifier), {!PatriciaTree.SetNode} (node optimized for set,
  doesn't store the [unit] value) and {!PatriciaTree.WeakSetNode}.
  Use the functors {!PatriciaTree.MakeCustomHeterogeneous} and {!PatriciaTree.MakeCustom} to build
  maps using these nodes, or any other custom nodes.}
}
{1 Examples}
{2 Homogeneous map}
Here is a small example of a non-generic map:
{[
(** Create a key struct *)
module Int (*: PatriciaTree.KEY*) = struct
  type t = int
  let to_int x = x
end
(** Call the map and/or set functors *)
module IMap = PatriciaTree.MakeMap(Int)
module ISet = PatriciaTree.MakeSet(Int)
(** Use all the usual map operations *)
let map =
  IMap.empty |&gt;
  IMap.add 1 &quot;hello&quot; |&gt;
  IMap.add 2 &quot;world&quot; |&gt;
  IMap.add 3 &quot;how do you do?&quot;
  (* Also has an [of_list] and [of_seq] operation for initialization *)
let _ = IMap.find 1 map (* &quot;hello&quot; *)
let _ = IMap.cardinal map (* 3 *)
(** The strength of Patricia Tree is the speedup of operation on multiple maps
    with common subtrees. *)
let map2 =
  IMap.idempotent_inter_filter (fun _key _l _r -&gt; None)
  (IMap.add 4 &quot;something&quot; map) (IMap.add 5 &quot;something else&quot; map)
let _ = map == map2 (* true *)
(* physical equality is preserved as much as possible, although some intersections
   may need to build new nodes and won't be fully physically equal, they will
   still share subtrees if possible. *)
(** Many operations preserve physical equality whenever possible *)
let _ = (IMap.add 1 &quot;hello&quot; map) == map (* true: already present *)
(** Example of cross map/set operation: only keep the bindings of [map]
    whose keys are in a given set *)
let set = ISet.of_list [1; 3]
module CrossOperations = IMap.WithForeign(ISet.BaseMap)
let restricted_map = CrossOperations.nonidempotent_inter
  { f = fun _key value () -&gt; value } map set
]}
{2 Heterogeneous map}
{[
(** Very small typed expression language *)
type 'a expr =
  | G_Const_Int : int -&gt; int expr
  | G_Const_Bool : bool -&gt; bool expr
  | G_Addition : int expr * int expr -&gt; int expr
  | G_Equal : 'a expr * 'a expr -&gt; bool expr
module Expr : PatriciaTree.HETEROGENEOUS_KEY with type 'a t = 'a expr = struct
  type 'a t = 'a expr
  (** Injective, so long as expression are small enough
      (encodes the constructor discriminant in two lowest bits).
      Ideally, use a hash-consed type, to_int needs to be fast *)
  let rec to_int : type a. a expr -&gt; int = function
    | G_Const_Int i -&gt;   0 + 4*i
    | G_Const_Bool b -&gt;  1 + 4*(if b then 1 else 0)
    | G_Addition(l,r) -&gt; 2 + 4*(to_int l mod 10000 + 10000*(to_int r))
    | G_Equal(l,r) -&gt;    3 + 4*(to_int l mod 10000 + 10000*(to_int r))
  (** Full polymorphic equality *)
  let rec polyeq : type a b. a expr -&gt; b expr -&gt; (a, b) PatriciaTree.cmp =
    fun l r -&gt; match l, r with
    | G_Const_Int l, G_Const_Int r -&gt; if l = r then Eq else Diff
    | G_Const_Bool l, G_Const_Bool r -&gt; if l = r then Eq else Diff
    | G_Addition(ll, lr), G_Addition(rl, rr) -&gt; (
        match polyeq ll rl with
        | Eq -&gt; polyeq lr rr
        | Diff -&gt; Diff)
    | G_Equal(ll, lr), G_Equal(rl, rr) -&gt;    (
        match polyeq ll rl with
        | Eq -&gt; (match polyeq lr rr with Eq -&gt; Eq | Diff -&gt; Diff) (* Match required by typechecker *)
        | Diff -&gt; Diff)
    | _ -&gt; Diff
end
(** Map from expression to their values: here the value only depends on the type
    of the key, not that of the map *)
module EMap = PatriciaTree.MakeHeterogeneousMap(Expr)(struct type ('a, _) t = 'a end)
(** You can use all the usual map operations *)
let map : unit EMap.t =
  EMap.empty |&gt;
  EMap.add (G_Const_Bool false) false |&gt;
  EMap.add (G_Const_Int 5) 5 |&gt;
  EMap.add (G_Addition (G_Const_Int 3, G_Const_Int 6)) 9 |&gt;
  EMap.add (G_Equal (G_Const_Bool false, G_Equal (G_Const_Int 5, G_Const_Int 7))) true
let _ = EMap.find (G_Const_Bool false) map (* false *)
let _ = EMap.cardinal map (* 4 *)
(** Fast operations on multiple maps with common subtrees. *)
let map2 =
  EMap.idempotent_inter_filter
    { f = fun _key _l _r -&gt; None } (* polymorphic 1rst order functions are wrapped in records *)
    (EMap.add (G_Const_Int 0) 8 map)
    (EMap.add (G_Const_Int 0) 9 map)
]}
{1 Release status}
This should be close to a stable release. It is already being
used as part of a larger project successfully, and this usage as helped us mature
the interface. As is, we believe the project is usable, and we don't anticipate
any major change before 1.0.0. We didn't commit to a stable release straight
away as we would like a bit more time using this library before doing so.

{1 Known issues}
There is a bug in the OCaml typechecker which prevents us from directly
defining non-generic maps as instances of generic maps. To avoid this, non-generic maps
use a separate value type (instead of just using ['b])
{[
type (_, 'b) snd = Snd of 'b [@@unboxed]
]}
It should not incur any extra performance cost as it is unboxed, but can appear
when manipulating non-generic maps.
For more details about this issue, see {{: https://discuss.ocaml.org/t/weird-behaviors-with-first-order-polymorphism/13783}the OCaml discourse discussion}.
{1 Comparison to other OCaml libraries}
{2 ptmap and ptset}
There are other implementations of Patricia Tree in OCaml, namely
{{: https://github.com/backtracking/ptmap}ptmap} and
{{: https://github.com/backtracking/ptset}ptset}, both by J.C. Filliatre.
These are smaller and closer to OCaml's built-in [Map] and [Set], however:
- Our library allows using any type [key] that comes with an injective [to_int]
  function, instead of requiring [key = int].
- We support generic types for keys/elements.
- We support operations between sets and maps of different types.
- We use a big-endian representation, allowing easy access to min/max elements of
  maps and trees.
- Our interface and implementation tries to maximize the sharing between different
  versions of the tree, and to benefit from this memory sharing. Theirs do not.
- These libraries work with older version of OCaml ([&gt;= 4.05] I believe), whereas
  ours requires OCaml [&gt;= 4.14] (for the new interface of [Ephemeron] used in
  {!PatriciaTree.WeakNode}).
{2 dmap}
Additionally, there is a dependent map library: {{: https://gitlab.inria.fr/bmontagu/dmap}dmap},
which gave us the idea of making our PatriciaTree dependent.
It allows creating type safe dependent maps similar to our heterogeneous maps.
However, its maps aren't Patricia trees. They are binary trees build using a
(polymorphic) comparison function, similarly to the maps of the standard library.
Another difference is that the type of values in the map is independent from the
type of the keys, allowing keys to be associated with different values in different maps.
i.e. we map ['a key] to any [('a, 'b) value] type, whereas dmap only maps ['a key]
to ['a] or ['a value].
[dmap] also works with OCaml [&gt;= 4.12], whereas we require OCaml [&gt;= 4.14].
{1 Contributions and bug reports}
Any contributions are welcome!
You can report any bug, issues, or desired features using the {{: https://github.com/codex-semantics-library/patricia-tree/issues}Github issue tracker}.
Please include OCaml, dune, and library version information in you bug reports.
If you want to contribute code, feel free to fork {{: https://github.com/codex-semantics-library/patricia-tree}the repository on Github}
and open a pull request. By doing so you agree to release your code under this
project's license ({{: https://choosealicense.com/licenses/lgpl-2.1/}LGPL-2.1}).

There is no imposed coding style for this repository, here are just a few guidelines and conventions:
- Module type names should use [SCREAMING_SNAKE_CASE].
- Module and functor names use [PascalCase], functors names start with [Make].
- Even though the library implements homogeneous maps as a specialization of
  heterogeneous ones, the naming convention is that no prefix means homogeneous,
  and all heterogeneous objects are prefixed with [heterogeneous].
- Please document any new functions in the interface, using {{: https://v2.ocaml.org/manual/ocamldoc.html#s%3Aocamldoc-comments}ocamldoc style comments}.
- Please consider adding test for new features/fixed bugs if at all possible.
  This library uses a {{: https://www.ocaml.org/p/quickcheck/latest/doc/QuickCheck/index.html}QuickCheck} framework for tests.
</code></a></p></li></ul></div></body></html>
